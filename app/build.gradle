plugins {
    id 'java'
    id 'application' 
    id 'io.github.goooler.shadow' version '8.1.8'
}

group = 'com.cvetti'
version = '1.0'

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    // Use JUnit Jupiter for testing
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    // Guava library
    implementation 'com.google.guava:guava:32.1.1-jre'
    implementation 'org.json:json:20231013'
    implementation 'com.mysql:mysql-connector-j:9.0.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'com.cvetti.server.Server'
}

tasks.register('shadowServer', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveClassifier.set('server')
    archiveFileName.set("server-fat.jar")

    manifest {
        attributes(
            'Main-Class': 'com.cvetti.server.Server'
        )
    }

    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
}

tasks.register('shadowClient', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveClassifier.set('client')
    archiveFileName.set("client-fat.jar")

    manifest {
        attributes(
            'Main-Class': 'com.cvetti.client.ClientApp' // coloque seu main aqui
        )
    }

    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
}

tasks.named('test') {
    useJUnitPlatform()
}
